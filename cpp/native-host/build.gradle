plugins {
    id 'org.jetbrains.kotlin.jvm'
}

dependencies {
    testImplementation project(":internal-testing")
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_jupiter_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
}

test {
    dependsOn ':cpp:native-host:native-host-simulation:jar', ':cpp:native-host:native-host-debug:jar', ':cpp:native-host:native-host-release:jar'
    doFirst {
        systemProperty 'simulation-jar.path', tasks.getByPath(':cpp:native-host:native-host-simulation:jar').outputs.files.asPath
        systemProperty 'debug-jar.path', tasks.getByPath(':cpp:native-host:native-host-debug:jar').outputs.files.asPath
        systemProperty 'release-jar.path', tasks.getByPath(':cpp:native-host:native-host-release:jar').outputs.files.asPath
    }
}

def cppBuildDir = project(":cpp").buildDir

subprojects { Project project ->
    apply plugin: 'java'

    def type = project.name.drop("native-host-".size()).capitalize()
    project.tasks.getByName("jar") { Jar task ->
        task.dependsOn(":cpp:compileLinuxSgx$type")
        task.dependsOn(":cpp:compileHost$type")
        def linuxSgxBuildDir = "$cppBuildDir/$type/linux-sgx/src/linux-sgx/build/linux"
        def linuxAzurePluginDir = "$cppBuildDir/../../azure-plugin"
        def linuxSgxQuoteGenBuildDir = "$cppBuildDir/$type/linux-sgx/src/linux-sgx/external/dcap_source/QuoteGeneration/build/linux"
        def hostBuildDir = "$cppBuildDir/$type/jvm-host"
        def hostSharedBuildDir = "$cppBuildDir/$type/jvm-host-shared"
        def libDir = "/usr/lib/x86_64-linux-gnu"
        def simSuffix = type == "Simulation" ? "_sim" : ""
        task.into "com/r3/conclave/host-libraries/$type", {
            from (linuxSgxBuildDir) {
                include "libsgx_enclave_common.so"
                rename "libsgx_enclave_common.so", "libsgx_enclave_common.so.1"
                include "libsgx_epid${simSuffix}.so"
                rename "libsgx_epid${simSuffix}.so", "libsgx_epid${simSuffix}.so.1"
                include "libsgx_launch${simSuffix}.so"
                rename "libsgx_launch${simSuffix}.so", "libsgx_launch${simSuffix}.so.1"
                include "libsgx_urts${simSuffix}.so"
                include "libsgx_uae_service${simSuffix}.so"
                include "libsgx_capable.so"
                include "libsgx_qe3.signed.so"
                include "libsgx_pce.signed.so"
            }
            from (hostBuildDir) {
                include "libjvm_host.so"
            }
            from (hostSharedBuildDir) {
                include "libjvm_host_shared.so"
            }
            from (libDir) {
                include "libcrypto.so"
            }
            from (linuxSgxQuoteGenBuildDir) {
                include "libsgx_qe3_logic.so"
                include "libsgx_pce_logic.so"
                include "libsgx_dcap_ql.so"
                rename "libsgx_dcap_ql.so", "libsgx_dcap_ql.so.1"
            }
	        from (linuxAzurePluginDir) {
                include "libdcap_quoteprov.so"
                rename "libdcap_quoteprov.so", "libdcap_quoteprov.so.1"
            }
            if (type != "Simulation") {
                from (libDir) {
                    include "libprotobuf.so"
                }
            }
        }
    }
}

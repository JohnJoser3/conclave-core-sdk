import javax.inject.Inject
import java.nio.file.Paths
import java.nio.file.Files

apply plugin: "base"

def types = ["Simulation", "Debug", "Release"]

task generateHostJniHeaders(type: Javah, constructorArgs: ['com.r3.conclave.host.internal.Native']) {
    dependsOn ":conclave-host:jar"
    inputJar = tasks.getByPath(":conclave-host:jar").archiveFile.get().asFile
    outputHeader = file("$projectDir/jvm-host/include/host_jni.h")
}

task generateHostSharedJniHeaders(type: Javah, constructorArgs: ['com.r3.conclave.host.internal.NativeShared']) {
    dependsOn ":conclave-host:jar"
    inputJar = tasks.getByPath(":conclave-host:jar").archiveFile.get().asFile
    outputHeader = file("$projectDir/jvm-host-shared/include/host_jni_shared.h")
}

task generateEnclaveJniHeaders(type: Javah, constructorArgs: ['com.r3.conclave.enclave.internal.Native']) {
    dependsOn ":conclave-enclave:jar"
    inputJar = tasks.getByPath(":conclave-enclave:jar").archiveFile.get().asFile
    outputHeader = file("$projectDir/jvm-enclave-common/include/enclave_jni.h")
}

/*
 * linux-sgx, native-substratevm and most of the stubs, edl libraries etc,
 * only need a debug and release versions, however they are being built
 * as part of the createCmakeBuild task, which builds the 3 flavors.
 */
String buildDirectory(type) { return "$buildDir/$type" }
static String createCmakeTaskName(type) { return "createCmakeBuild$type" }
static String compileTaskNameEnclave(type) { return "compileEnclave$type" }
static String compileTaskNameHost(type) { return "compileHost$type" }
static String compileTaskNameHostShared(type) { return "compileHostShared$type" }
static String compileTaskNameLinuxSgx(type) { return "compileLinuxSgx$type" }
static String compileTaskNameSubstrateVMLib(type) { return "compileSubstrateVMLib$type" }
static String runCppUnitTestsTaskName(type) { return "runCppUnitTests$type" }
static String symlinkTaskName(from, to) { return "symlink${from}To$to" }
static String cmakeBuildType(type) { return "-DCMAKE_BUILD_TYPE=$type" }

def symlinks = [[from: "Release", to: "Simulation"], [from: "Release", to: "Debug"], [from: "Debug", to: "Simulation"]]

for (symlink in symlinks) {
    // These variables below are needed because if we reference "symlink" inside the execution of "doFirst" process yields
    // the last "symlink" used, and not those used during the task creation.
    def from = symlink.from
    def to = symlink.to

    // Create symlinks to reduce linux-sgx sdk build time.
    tasks.create(symlinkTaskName(from, to)) {
        dependsOn(compileTaskNameLinuxSgx(from))
        doFirst {
            mkdir buildDirectory(to)
            try {
                Files.createSymbolicLink(Paths.get("${buildDirectory(to)}/linux-sgx"), Paths.get("${buildDirectory(from)}/linux-sgx"))
            } catch (ignored) {
            }
        }
        outputs.dir(buildDirectory(to))
    }
}

for (type in types) {
    // Use Cmake to create build
    tasks.create(createCmakeTaskName(type), Exec) {
        workingDir(buildDirectory(type))

        commandLine("/usr/bin/env", "cmake", cmakeBuildType(type), projectDir)

        inputs.files(
                "$projectDir/CMakeLists.txt",
                "$projectDir/*/CMakeLists.txt"
        )

        outputs.files(
                "${buildDirectory(type)}/Makefile"
        )
    }

    // Build the enclave object
    tasks.create(compileTaskNameEnclave(type), Exec) {
        dependsOn(compileTaskNameLinuxSgx(type), createCmakeTaskName(type), generateEnclaveJniHeaders)

        workingDir(buildDirectory(type))

        commandLine("/usr/bin/env", "cmake",
                "--build", buildDirectory(type),
                "--target", "jvm_enclave_avian"
        )
    }

    // Build the host object
    tasks.create(compileTaskNameHost(type), Exec) {
        dependsOn(compileTaskNameLinuxSgx(type), createCmakeTaskName(type), generateHostJniHeaders, compileTaskNameHostShared(type))

        workingDir(buildDirectory(type))

        commandLine("/usr/bin/env", "cmake",
                "--build", buildDirectory(type),
                "--target", "jvm_host"
        )
    }

    // Build the shared host object
    tasks.create(compileTaskNameHostShared(type), Exec) {
        dependsOn(compileTaskNameLinuxSgx(type), createCmakeTaskName(type), generateHostSharedJniHeaders)

        workingDir(buildDirectory(type))

        commandLine("/usr/bin/env", "cmake",
                "--build", buildDirectory(type),
                "--target", "jvm_host_shared"
        )
    }

    tasks.create(compileTaskNameLinuxSgx(type), Exec) {
        def buildSgxDebug = project.hasProperty("linux-sgx-debug")
        if (buildSgxDebug && type == "Simulation") {
            dependsOn(symlinkTaskName("Debug", type))
            commandLine("echo", "Linux SGX $type is a symlink from Debug.")
        } else if (!buildSgxDebug && (type == "Simulation" || type == "Debug")) {
            dependsOn(symlinkTaskName("Release", type))
            commandLine("echo", "Linux SGX $type is a symlink from Release.")
        } else {
            dependsOn(createCmakeTaskName(type))
            workingDir(buildDirectory(type))
            commandLine("/usr/bin/env", "cmake",
                    "--build", buildDirectory(type),
                    "--target", "linux-sgx-ext"
            )
        }
    }

    tasks.create(compileTaskNameSubstrateVMLib(type), Exec) {
        dependsOn(createCmakeTaskName(type))

        workingDir(buildDirectory(type))

        commandLine("/usr/bin/env", "cmake",
                "--build", buildDirectory(type),
                "--target", "substratevm"
        )
    }

    // Run the cpp unit tests
    tasks.create(runCppUnitTestsTaskName(type), Exec) {
        dependsOn(createCmakeTaskName(type))

        workingDir(buildDirectory(type))

        commandLine("/usr/bin/env", "cmake",
                "--build", buildDirectory(type),
                "--target", "build_tests_and_run"
        )
    }
}

task cleanAvian(type: Delete) {
    delete "$projectDir/avian/build"
}

clean.finalizedBy cleanAvian

class Javah extends DefaultTask {
    final String nativeClass

    @OutputFile
    File outputHeader
    @InputFile
    File inputJar

    @Inject
    Javah(String nativeClass) {
        this.nativeClass = nativeClass
    }

    @TaskAction
    def run() {
        project.exec { spec ->
            spec.workingDir(temporaryDir)
            spec.commandLine("/usr/bin/env", "javah",
                    "-v",
                    "-o", outputHeader,
                    "-cp", inputJar,
                    nativeClass
            )
        }
    }
}

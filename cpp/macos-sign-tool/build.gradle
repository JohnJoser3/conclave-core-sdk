import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'org.jetbrains.kotlin.jvm'
}

dependencies {
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_jupiter_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
}

task copyToSourceTree(type: Copy) {
    dependsOn ":cpp:linux-sgx:buildSgxSign"
    from "${project(":cpp:linux-sgx").buildDir}/src/linux-sgx/sdk/sign_tool/SignTool/sgx_sign"
    into projectDir
}

/**
 * When building on macOS it will trigger the compilation of sign-tool.
 * On Linux, it will only pickup and package the checked-in file built on macOS.
 */
jar {
    if (Os.isFamily(Os.FAMILY_MAC)) {
        dependsOn copyToSourceTree
    }
    into "com/r3/conclave/sign-tool/macos", {
        from(projectDir) {
            include "sgx_sign"
        }
    }
}

test {
    dependsOn ':cpp:macos-sign-tool:jar'
    doFirst {
        systemProperty 'macos-sign-tool.path', tasks.getByPath(':cpp:macos-sign-tool:jar').outputs.files.asPath
    }
}
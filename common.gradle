allprojects {
    group = "com.r3.conclave"
    version = conclave_version
}

subprojects {
    ext.conclave_spid = '***REMOVED***'
    ext.attestation_key = '***REMOVED***'

    tasks.withType(AbstractCompile) {
        if (it.class.name.startsWith('org.jetbrains.kotlin.gradle.tasks.KotlinCompile')) {
            kotlinOptions {
                freeCompilerArgs = ['-Xjvm-default=all']
                javaParameters = true
            }
        }
    }

    tasks.withType(Test) {
        useJUnitPlatform()

        systemProperty 'java.io.tmpdir', buildDir.absolutePath
        systemProperty 'conclave.spid', conclave_spid
        systemProperty 'conclave.attestation-key', attestation_key

        failFast = true
        testLogging {
            exceptionFormat = 'full'
            showStandardStreams = true
        }

        beforeTest { descriptor ->
            logger.lifecycle("Starting> {}.{} ({})", descriptor.classDisplayName, descriptor.displayName, descriptor.id)
        }
        afterTest { descriptor ->
            logger.lifecycle("Ending> {}.{} ({})", descriptor.classDisplayName, descriptor.displayName, descriptor.id)
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

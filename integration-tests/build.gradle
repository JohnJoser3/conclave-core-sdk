plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0' apply false
}

apply from: '../versions.gradle'
apply from: '../common.gradle'

subprojects {
    ext.enclaveMode = findProperty("enclaveMode")?.toString()?.toLowerCase() ?: "simulation"
    ext.attestationMode = findProperty("attestationMode") ?: "epid"
    repositories {
        switch (System.getenv('USE_MAVEN_REPO')?.toLowerCase()) {
            case "artifactory":
                maven {
                    url = "https://software.r3.com/artifactory/sgxjvm"
                    credentials {
                        username = System.getenv('OBLIVIUM_MAVEN_USERNAME')
                        password = System.getenv('OBLIVIUM_MAVEN_PASSWORD')
                    }
                }
                break
            default:
                maven {
                    url = "${rootProject.projectDir}/../build/distributions/conclave-sdk-$conclave_version/repo"
                }
        }
        mavenCentral()
    }

    tasks.withType(Test) {
        useJUnitPlatform {
            if (findProperty("slowTests") == null) {
                excludeTags("slow")
            } else {
                includeTags("slow")
            }
        }
    }

}



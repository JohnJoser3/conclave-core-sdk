apply from: '../versions.gradle'
apply from: '../common.gradle'

subprojects {
    apply plugin: 'java'

    ext.enclaveMode = findProperty("enclaveMode")?.toString()?.toLowerCase() ?: "simulation"
    ext.attestationMode = findProperty("attestationMode") ?: "epid"

    repositories {
        maven {
            url = "${rootProject.projectDir}/../build/distributions/conclave-sdk-$conclave_version/repo"
        }
        maven {
            url = "https://software.r3.com/artifactory/sgxjvm"
            credentials {
                username = System.getenv('OBLIVIUM_MAVEN_USERNAME')
                password = System.getenv('OBLIVIUM_MAVEN_PASSWORD')
            }
        }
        mavenCentral()
    }

    // This will also configure Kotlin: https://kotlinlang.org/docs/gradle.html#gradle-java-toolchains-support
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(Test) {
        useJUnitPlatform {
            if (findProperty("slowTests") == null) {
                excludeTags("slow")
            } else {
                includeTags("slow")
            }
        }
    }
}

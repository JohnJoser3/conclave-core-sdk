plugins {
    id 'application'
}

application {
    // conclave-web-host provides us with the main method to use.
    mainClassName = "com.r3.conclave.host.web.EnclaveWebHost"
}

// Override the default (mock) with -PenclaveMode=
def mode = findProperty("enclaveMode")?.toString()?.toLowerCase() ?: "mock"

// Create a task that can be used for generating signing materials.
// Only required if you building singed/release version.
tasks.register("prepareForSigning") {
    it.dependsOn(":enclave:generateEnclaveSigningMaterial${mode.capitalize()}")
}

dependencies {
    // *your* enclave
    runtimeOnly project(path: ":enclave", configuration: mode)
    // Use the host web server for receiving and sending mail to the clients.
    runtimeOnly "com.r3.conclave:conclave-web-host:$conclaveVersion"

    // Enable unit tests
    testImplementation "com.r3.conclave:conclave-host:$conclaveVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
}

plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'
}

gradlePlugin {
    plugins {
        sgxHostPlugin {
            id = 'com.r3.sgx.host'
            implementationClass = 'com.r3.sgx.plugin.host.SgxHostPlugin'
        }
    }
}

tasks.withType(Test) {
    // Tell the tests where Gradle's current module cache is.
    // We need the tests to share this module cache to prevent the
    // Gradle Test-Kit from downloading its own copy of Kotlin etc.
    systemProperty 'test.gradle.user.home', project.gradle.gradleUserHomeDir
}

dependencies {
    // This plugin will use Gradle's own built-in Kotlin library at runtime.
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.bmuschko:gradle-docker-plugin:5.2.0"
    implementation project(':plugin-enclave-gradle')
}

jar {
    manifest {
        attributes("Conclave-Version": rootProject.version.toString())
        def url = rootProject.ext.environment.OBLIVIUM_CONTAINER_REGISTRY_URL
        // Handle syncing an IDE outside the devenv environment.
        if (url == null)
            url = "localhost:5000"
        attributes("Conclave-Docker-Registry": url)
    }
}

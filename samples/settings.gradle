pluginManagement {
    apply from: '../versions.gradle'

    repositories {
        gradlePluginPortal()
        mavenCentral()
        jcenter()
        // When USE_MAVEN_REPO is not set the gradle composite build is used
        switch (System.getenv('USE_MAVEN_REPO')?.toLowerCase()) {
            case "artifactory":
                maven {
                    url = "https://software.r3.com/artifactory/sgxjvm"
                    credentials {
                        username = System.getenv('OBLIVIUM_MAVEN_USERNAME')
                        password = System.getenv('OBLIVIUM_MAVEN_PASSWORD')
                    }
                }
                break
            case "sdk":
                maven {
                    url = "${rootProject.projectDir}/../build/distributions/conclave-sdk-$conclave_version/repo"
                }
                break
        }
    }
    resolutionStrategy {
        if (System.getenv('USE_MAVEN_REPO') != null) {
            eachPlugin {
                if (requested.id.id == 'com.r3.conclave.enclave') {
                    useModule("com.r3.conclave:plugin-enclave-gradle:$conclave_version")
                }
            }
        }
    }

    plugins {
        id 'org.jetbrains.kotlin.jvm' version kotlin_version apply false
        id 'com.google.protobuf' version protobuf_plugin_version apply false
    }
}

if (System.getenv('USE_MAVEN_REPO') == null) {
    includeBuild("..") {
        name = "sgxjvm"
    }
}

include ':jvm-perf:benchmarks'
include ':jvm-perf:avian-enclave-simulation'
include ':jvm-perf:graalvm-enclave-simulation'
include ':jvm-perf:avian-enclave-debug'
include ':jvm-perf:graalvm-enclave-debug'
include ':jvm-perf:jvm-perf'

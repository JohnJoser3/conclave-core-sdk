/**
 * This module contains the enclave code which handles and runs user code in the DJVM.
 */
plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.r3.conclave.enclave'
}

configurations {
    jdkRt.resolutionStrategy {
        // Always check the repository for a newer SNAPSHOT.
        cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    implementation project(":djvm:djvm-common")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.r3.conclave:conclave-enclave"
    implementation "net.corda.djvm:corda-djvm:$djvm_version"
    implementation "com.r3.conclave:enclave-utils"

    jdkRt "net.corda:deterministic-rt:$deterministic_rt_version"
}

processResources {
    with copySpec {
        from configurations.jdkRt.asPath
        rename { 'deterministic-rt.zip' } // Renamed to zip so shadowJar doesn't expand it: https://github.com/johnrengelman/shadow/issues/111
    }
}

conclave {
    productID = 1
    revocationLevel = 0

    release {
        // By default release enclaves will use a 2 step external signing process.
        // Set the sample to use a dummy key.
        signingType = dummyKey
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61' apply false
    id 'com.google.protobuf' version '0.8.9' apply false
    id 'com.r3.sgx.host' apply false
    id "com.github.ben-manes.versions" version "0.27.0"
}

import static org.gradle.api.JavaVersion.*
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

def sgxModeProperty = project.property('sgx_mode')
subprojects {
    group = "com.r3.sgx"

    ext {
        sgxMode = sgxModeProperty
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url = "https://software.r3.com/artifactory/sgxjvm"
        }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = VERSION_1_8
        targetCompatibility = VERSION_1_8
    }

    tasks.withType(KotlinCompile) {
        kotlinOptions {
            jvmTarget = VERSION_1_8
            apiVersion = '1.3'
            languageVersion = '1.3'
            freeCompilerArgs += [
                '-Xjvm-default=enable',
                '-XXLanguage:+NewInference'
            ]
        }
    }

    tasks.withType(Test) {
        systemProperty 'sgx.mode', sgxModeProperty
        reports.junitXml.destination = file("$buildDir/$sgxModeProperty/test-results/xml")
        reports.html.destination = file("$buildDir/$sgxModeProperty/test-results/html")
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

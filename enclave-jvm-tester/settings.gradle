pluginManagement {
    apply from: '../versions.gradle'

    repositories {
        maven {
            gradlePluginPortal()
            mavenCentral()
            jcenter()
            url = "https://software.r3.com/artifactory/sgxjvm"
            credentials {
                username = System.getenv('OBLIVIUM_MAVEN_USERNAME')
                password = System.getenv('OBLIVIUM_MAVEN_PASSWORD')
            }
        }
    }
    resolutionStrategy {
        def version = System.getenv('PROJECT_VERSION')
        if (version != null) {
            if (version.empty) {
                version = default_conclave_version
            }
            eachPlugin {
                if (requested.id.id == 'com.r3.conclave.enclave') {
                    useModule("com.r3.conclave:plugin-enclave-gradle:$version")
                }
            }
        }
    }

    plugins {
        id 'org.jetbrains.kotlin.jvm' version kotlin_version apply false
        id 'com.google.protobuf' version protobuf_plugin_version apply false
    }
}

def projectVersion = System.getenv('PROJECT_VERSION')
if (projectVersion == null) {
    includeBuild("..") {
        name = "sgxjvm"
    }
}

include ':api'
include ':enclave'
include ':host'
include ':djvm:djvm-test-utils'
include ':djvm:djvm-asserters'
include ':djvm:djvm-tests'
include ':djvm:djvm-tests-auxiliary'
include ':djvm:djvm-proto'
include ':tests:maths'

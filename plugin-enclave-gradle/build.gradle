import static org.gradle.api.JavaVersion.VERSION_1_8

plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'
}

gradlePlugin {
    plugins {
        enclavePlugin {
            id = 'com.r3.conclave.enclave'
            implementationClass = 'com.r3.conclave.plugin.enclave.gradle.GradleEnclavePlugin'
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = VERSION_1_8
        apiVersion = '1.3'
        languageVersion = '1.3'
    }
}

dependencies {
    // This plugin will use Gradle's own built-in Kotlin library at runtime.
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"

    // We need conclave-testing to able to parse the enclave metadata file. This utility resides in conclave-testing
    // (in an internal package) for use by our tests. Technically we could have this in a separate module but then we'd
    // have to publish it in the SDK for what is just internal code.
    implementation project(":conclave-testing")
    implementation "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    implementation "io.github.classgraph:classgraph:$classgraph_version"
    implementation "org.bouncycastle:bcpkix-jdk15on:$bouncycastle_version"

    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"
    testImplementation "org.jetbrains.kotlin:kotlin-test:1.3.72"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_jupiter_version"
    testImplementation gradleTestKit()
}

jar {
    manifest {
        // This is used in the plugin to add implicit dependencies of the right version.
        attributes("Conclave-Version": rootProject.version.toString())
    }
}

test {
    // These tests take a long time to run. Only run them if configured to do so
    // via '-PrunPluginEnclaveGradleTests' being passed to Gradle
    if (!project.hasProperty('runPluginEnclaveGradleTests')) {
        exclude '*'
    } else {
        // The tests require the locally published SDK repo.
        dependsOn(':sdkRepo')
    }
    // Tell the tests where Gradle's current module cache is.
    // We need the tests to share this module cache to prevent the
    // Gradle Test-Kit from downloading its own copy of Kotlin etc.
    systemProperty 'test.gradle.user.home', project.gradle.gradleUserHomeDir
    systemProperty 'gradle.version', project.gradle.gradleVersion
}

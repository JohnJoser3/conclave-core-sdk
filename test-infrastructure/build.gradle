import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static org.gradle.api.JavaVersion.VERSION_1_8

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.30' apply false
    id 'com.google.protobuf' version '0.8.12' apply false
    id 'com.r3.sgx.host' apply false
}

def sgxModeProperty = project.property('sgx_mode')
subprojects {
    ext {
        sgxMode = sgxModeProperty
    }

    repositories {
        mavenCentral()
        // DJVM and determinist-rt
        maven { url 'https://software.r3.com/artifactory/corda-dependencies' }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = VERSION_1_8
        targetCompatibility = VERSION_1_8
    }

    tasks.withType(KotlinCompile) {
        kotlinOptions {
            jvmTarget = VERSION_1_8
            apiVersion = '1.3'
            languageVersion = '1.3'
            freeCompilerArgs = [ '-Xjvm-default=enable' ]
        }
    }

    tasks.withType(Test) {
        systemProperty 'sgx.mode', sgxModeProperty
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}